#!/usr/bin/env zsh
set -euo pipefail

usage() {
  cat >&2 <<EOF
Usage: ${0:t} [--keep-audio] input1 [input2 ...]
  Converts video files to HEVC (H.265) in an MP4 container (~40 Mbps).

Options:
  --keep-audio   Keep audio and convert it to AAC.
                 Default is to strip audio (video-only output).

Supported extensions: .webm .mp4 .mov .mkv .m4v
Outputs: <basename>_hevc40.mp4 in the same directory as each input.
EOF
  exit 1
}

keep_audio=false

# --- parse options
while (( $# )); do
  case "$1" in
    --keep-audio)
      keep_audio=true
      shift
      ;;
    -h|--help)
      usage
      ;;
    --*)
      echo "Unknown option: $1" >&2
      usage
      ;;
    *)
      break
      ;;
  esac
done

(( $# >= 1 )) || usage

# --- audio args
if $keep_audio; then
  # AAC for MP4
  audio_args=(-c:a aac -b:a 192k)
else
  audio_args=(-an)
fi

for infile in "$@"; do
  [[ -f "$infile" ]] || { echo "Skip (not a file): $infile" >&2; continue }

  ext="${${infile##*.}:l}"
  case "$ext" in
    webm|mp4|mov|mkv|m4v) ;;
    *)
      echo "Skip (unsupported extension .$ext): $infile" >&2
      continue
      ;;
  esac

  dir="${infile:h}"
  base="${infile:t}"
  base="${base%.*}"
  outfile="${dir}/${base}_hevc40.mp4"

  if $keep_audio; then
    opt_audio="with AAC audio"
  else
    opt_audio="video-only (no audio)"
  fi

  echo "Converting:"
  echo "  IN : $infile"
  echo "  OUT: $outfile"
  echo "  OPT: HEVC (H.265), ~40 Mbps, $opt_audio"

  ffmpeg -y -i "$infile" \
    "${audio_args[@]}" \
    -c:v libx265 \
    -preset slow \
    -b:v 40M \
    -maxrate 40M \
    -bufsize 80M \
    -tag:v hvc1 \
    -movflags +faststart \
    "$outfile"
done
