#!/bin/zsh
TAB_DIR="${TABLATURE_DIR:-$HOME/Music/Tablature}"

[[ -d "$TAB_DIR" && -r "$TAB_DIR" ]] || {
  echo "Error: Tablature dir invalid"
  exit 1
}

cd "$TAB_DIR" || exit 1

HEADER=$'↵ open · 󰘵 preview · 󰘵 r reveal · 󰉴 n rename files\n'

# ANSI colors
CYAN=$'\e[36m'
YELLOW=$'\e[33m'
RED=$'\e[31m'
RESET=$'\e[0m'

# Collect artist-title pairs
entries=()
max_artist_len=0

while IFS='|' read -r _mod file; do
  rel="${file#./}"
  base="${rel%.pdf}"
  if [[ "$base" == *" - "* ]]; then
    artist="${base%% - *}"
    title="${base#* - }"
    entries+="$rel|$artist|$title"
    ((${#artist} > max_artist_len)) && max_artist_len=${#artist}
  else
    entries+="$rel|$base|"
  fi
done < <(fd . -e pdf -t f -x stat -f "%m|%N" {} + 2>/dev/null | sort -rn -t'|' -k1)

# Output formatted
for entry in $entries; do
  rel_path="${entry%%|*}"
  rest="${entry#*|}"
  artist="${rest%%|*}"
  title="${rest#*|}"

  if [[ -n "$title" ]]; then
    padded_artist=$(printf "%-${max_artist_len}s" "$artist")
    echo -E "$rel_path"$'\t'"${CYAN}${padded_artist}${RESET}  ${YELLOW}${title}${RESET}"
  else
    echo -E "$rel_path"$'\t'"${RED}${artist}${RESET}"
  fi
done | fzf --margin="0%,0%" \
  --exact \
  --reverse --ansi --cycle --marker=" " \
  --pointer="󰋆" --border="horizontal" --border-label=" 󰎈 Tabs " \
  --header="$HEADER" --header-first --no-info \
  --delimiter='\t' --with-nth=2 \
  --bind 'alt-r:execute(open --reveal {1})' \
  --bind 'ctrl-/:toggle-preview' \
  --bind 'enter:execute(echo "󰄬 Opening: {1}" && open {1}&)' \
  --bind 'alt-enter:execute(echo "󰄛 Previewing: {1}" && qlmanage -p {1} > /dev/null 2>&1)' \
  --bind 'alt-n:execute(rename-tablature)'
